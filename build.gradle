plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.7'
	id 'io.spring.dependency-management' version '1.1.0'
}

subprojects {
	group = 'payment-service'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'
	targetCompatibility = '17'

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'

	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-aop'

		compileOnly 'org.projectlombok:lombok'

		runtimeOnly 'com.mysql:mysql-connector-j'

		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}

project(":core") {
	bootJar {
		enabled = false
	}

	test {
		useJUnitPlatform()
	}

	dependencies {

	}
}

project(":integration") {
	bootJar {
		enabled = true
	}

	dependencies {
		implementation(project(":core"))
		implementation 'com.github.iamport:iamport-rest-client-java:0.2.22'
		implementation'org.springframework.boot:spring-boot-starter-data-redis'
		implementation 'org.redisson:redisson-spring-boot-starter:3.17.6'
		implementation 'org.springframework.kafka:spring-kafka'
	}
}

project(":payment-api") {

	dependencies {
		implementation(project(":integration"))
		implementation(project(":core"))

		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

		testImplementation 'org.mockito:mockito-core'
		testImplementation 'com.squareup.okhttp3:mockwebserver'
	}

	jar {
		enabled = false
	}

	bootJar {
		enabled = true
	}

	test {
		useJUnitPlatform()
	}
}

project(":order-api") {

	dependencies {
		implementation(project(":integration"))
		implementation(project(":core"))
		implementation 'org.springframework.kafka:spring-kafka'
	}

	jar {
		enabled = false
	}

	bootJar {
		enabled = true
	}

	test {
		useJUnitPlatform()
	}
}